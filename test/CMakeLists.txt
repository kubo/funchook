cmake_minimum_required(VERSION 3.5)
project(libfunchook_test LANGUAGES C ASM)

if (CMAKE_SIZEOF_VOID_P MATCHES "4")
  set(FUNCHOOK_CPU "x86")
elseif (CMAKE_SIZEOF_VOID_P MATCHES "8")
  set(FUNCHOOK_CPU "x86_64")
else ()
  message(FATAL_ERROR "Unsupported architecture detected!")
endif ()

if (MSVC)
  enable_language(ASM_MASM)
  set(FUNCHOOK_ASM_SUFFIX _masm.asm)
else ()
  set(FUNCHOOK_ASM_SUFFIX _gas.S)
endif ()

add_library(funchook_test SHARED libfunchook_test.c libfunchook_test_${FUNCHOOK_CPU}${FUNCHOOK_ASM_SUFFIX})
if (MSVC)
  set_target_properties(funchook_test PROPERTIES OUTPUT_NAME funchook_test_dll)
  if (CMAKE_SIZEOF_VOID_P MATCHES "4")
    set_source_files_properties(libfunchook_test_x86_masm.asm PROPERTIES COMPILE_FLAGS /safeseh)
  endif ()
else ()
  set_target_properties(funchook_test PROPERTIES SUFFIX ".so")
endif ()

set(FUNCHOOK_TEST_LIBS funchook_test)
if (UNIX)
  set(FUNCHOOK_TEST_LIBS ${FUNCHOOK_TEST_LIBS} dl)
endif ()

if (FUNCHOOK_BUILD_SHARED)
  add_executable(funchook_test_shared test_main.c ${FUNCHOOK_CPU}_test.S)
  target_link_libraries(funchook_test_shared PRIVATE funchook-shared ${FUNCHOOK_TEST_LIBS})
  if (UNIX)
    set_target_properties(funchook_test_shared PROPERTIES LINK_FLAGS -rdynamic)
  endif ()
  if (WIN32)
    add_custom_command(TARGET funchook_test_shared POST_BUILD COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_FILE:funchook-shared> $<TARGET_FILE_DIR:funchook_test_shared>)
  endif ()
  add_test(NAME funchook_test_shared COMMAND $<TARGET_FILE:funchook_test_shared>)
endif ()

if (FUNCHOOK_BUILD_STATIC)
  add_executable(funchook_test_static test_main.c ${FUNCHOOK_CPU}_test.S)
  target_link_libraries(funchook_test_static PRIVATE funchook-static ${FUNCHOOK_TEST_LIBS})
  if (UNIX)
    set_target_properties(funchook_test_static PROPERTIES LINK_FLAGS -rdynamic)
  endif ()
  add_test(NAME funchook_test_static COMMAND $<TARGET_FILE:funchook_test_static>)
endif ()
